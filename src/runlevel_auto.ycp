/**
 * File:	clients/runlevel_auto.ycp
 * Package:	Configuration of Runlevel
 * Summary:	Client for autoinstallation
 * Authors:	nashif@suse.de
 *
 * $Id$
 *
 * This is a client for autoinstallation. It takes its arguments,
 * goes through the configuration and return the setting.
 * Does not do any changes to the configuration.
 */

/**
 * @param function to execute
 * @param map/list of RunlevelEd settings
 * @return map edited settings, Summary or boolean on success depending on called function
 * @example map mm = $[ "FAIL_DELAY" : "77" ];
 * @example map ret = WFM::CallFunction ("RunlevelEd_auto", [ "Summary", mm ]);
 */

{
textdomain "runlevel";

y2milestone("----------------------------------------");
y2milestone("Runlevel auto started");

import "RunlevelEd";
import "Progress";
import "Wizard";

include "wizard/sequencer.ycp";
include "runlevel/ui.ycp";

any ret = nil;
string func = "";
map param = $[];

/* Check arguments */
if(size(Args()) > 0 && is(Args(0), string)) {
	func = WFM::Args(0);
	if(size(Args()) > 1 && is(Args(1), map))
		param = WFM::Args(1);
}
y2debug("func=%1", func);
y2debug("param=%1", param);



// you know what this means
boolean do_abort_now = false;

// We read service status when dialog with services is shown.
// We read status for services taken from list of services (service_list)
// and then update map services.
integer fetching_service_index = 0;
// when fetching_service_status becomes false, we stop fetching services
boolean fetching_service_status = false;

string current_service = "";

// index into table column for each runlevel
map runlevel2tableindex = $[];


// We take data from underlying system
// RunlevelEd::Read ();
integer id = 2;
foreach (`i, RunlevelEd::runlevels, ``{
		runlevel2tableindex = add (runlevel2tableindex, i, id);
		id = id + 1;
		});

//
// maps for sequencer
//
map aliases = $[
"start" :            ``(runMain ()),
	"complex" :          ``(runAutoDialog ()),
	];

map main_sequence = $[
"ws_start" : "start",
	"start" : $[ `next : `finish , `abort : `abort, `edit : "complex", ],
	"complex" : $[ `next : `finish, `abort : `abort ],
	];


/* Import Data*/
if(func == "Import") {
	ret = RunlevelEd::Import(param);
}
/* Create a  summary*/
else if(func == "Summary") {
	ret = RunlevelEd::Summary();
}
/* Reset configuration */
else if (func == "Reset") {
	RunlevelEd::Import($[]);
	ret = $[];
}
/* Change configuration (run AutoSequence) */
else if (func == "Change") {
	Wizard::CreateDialog ();
	Wizard::DisableAbortButton ();
	Wizard::RestoreHelp (getHelpProgress ());
	ret = WizardSequencer (aliases, main_sequence);
	UI::CloseDialog ();

}
/* Return actual state */
else if (func == "Export") {
	ret = RunlevelEd::Export();
}
/* Write givven settings */
else if (func == "Write") {
	import "Progress";
    RunlevelEd::Read ();
    RunlevelEd::Import (param);
	Progress::off();
	ret = RunlevelEd::Write();
	Progress::on();
}
/* Unknown function */
else {
	y2error("Unknown function: %1", func);
	ret = false;
}

y2debug("ret=%1", ret);
y2milestone("Runlevel auto finished");
y2milestone("----------------------------------------");

return ret;

/* EOF */
}
