/**
 * File:	clients/runlevel_proposal.ycp
 * Package:	Runlevel editor
 * Summary:	Default runlevel proposal
 * Authors:	Michal Svec <msvec@suse.cz>
 *
 * $Id$
 */

{

textdomain "runlevel";

/* The main () */
y2milestone("----------------------------------------");
y2milestone("Runlevel proposal started");
y2milestone("Arguments: %1", WFM::Args());

import "Label";
import "Progress";
import "RunlevelEd";
import "Summary";
import "Arch";

include "runlevel/ui.ycp";

string func = (string) WFM::Args(0);
map param = (map) WFM::Args(1);
map ret = $[];

define symbol RL() {

    UI::OpenDialog(`VBox(
	/* Heading label */
	`Heading( _("Set Default Runlevel") ),
	`VSpacing( 0.7 ),
	    /* ComboBox label */
	    `ComboBox (`id (`default_rl), `opt (`hstretch), _("&Default Runlevel:"), getDefaultPicker (`proposal)),
	`VSpacing( 0.5 ),
	`HBox(
	    `HStretch(),
	    `HWeight(1, `PushButton(`id(`ok), `opt(`default, `key_F10),
	    Label::OKButton() )),
	    `HStretch(),
	    `HWeight(1, `PushButton(`id(`cancel), `opt (`key_F9),
	    Label::CancelButton())),
	    `HStretch()
	)
    ));

    any ret = UI::UserInput();
    if(ret == `ok) {
	RunlevelEd::default = (string) UI::QueryWidget(`id(`default_rl), `Value);
	UI::CloseDialog();
	return `next;
    }
    UI::CloseDialog();
    return `back;
}

/* create a textual proposal */
if(func == "MakeProposal") {

    // #35292: force_reset is only triggered manually. better always recheck.
    if (true) {
	RunlevelEd::proposal_valid = true;
	// some architectures don't have X at all
	boolean x = Arch::x11_setup_needed;
	if (x)
	{
	    // Check if X11 package is installed.
	    boolean x11_installed = Pkg::IsProvided("XFree86");
	    if (!x11_installed)
	    {
		// is it selected for installation
		x11_installed = Pkg::IsSelected ("XFree86");
	    }
	    y2milestone ("x11_installed: <%1>", x11_installed );
	    x = x11_installed;
	}
	RunlevelEd::default = x? "5": "3";
    }
    string proposal = RunlevelEd::ProposalSummary ();

    ret = $[
	"preformatted_proposal" : proposal,
	"warning_level" : nil,
	"warning" : nil,
    ];
}
/* run the module */
else if(func == "AskUser") {
    map stored = RunlevelEd::Export();
    symbol result = RL(); //(symbol) WFM::CallFunction("runlevel", [.propose]);
    if(result != `next) RunlevelEd::Import(stored);
    y2debug("stored=%1", stored);
    y2debug("result=%1", result);
    ret = $[ "workflow_sequence" : result ];
}
/* create titles */
else if(func == "Description") {
    ret = $[
	/* Rich text title */
	"rich_text_title" : _("Default Runlevel"),
	/* MenuButton title */
	"menu_title" : _("Default &Runlevel"),
	"id" : "runlevel",
    ];
}
/* write the proposal */
else if(func == "Write") {
    y2milestone ("Not writing yet, will be done in inst_finish");
}
/* unknown function */
else {
    y2error("unknown function: %1", func);
}

/* Finish */
y2debug("ret=%1",ret);
y2milestone("Runlevel proposal finished");
y2milestone("----------------------------------------");
return ret;

/* EOF */
}
