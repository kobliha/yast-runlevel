/**
 * File:
 *  runlevel.ycp
 * Module:
 *  Runlevel editor.
 * Summary:
 *  Main file of runlevel editor.
 * Authors:
 *  Petr Blahos <pblahos@suse.cz>, 2001
 *
 * $Id$
 *
 * Main file of runlevel editor.
 */
{
    textdomain "runlevel";

    import "RunlevelEd";
    import "Progress";
    import "Wizard";
    import "Popup";

    include "wizard/sequencer.ycp";
    include "runlevel/ui.ycp";


    // you know what this means
    boolean do_abort_now = false;

    // We read service status when dialog with services is shown.
    // We read status for services taken from list of services (service_list)
    // and then update map services.
    integer fetching_service_index = 0;
    // when fetching_service_status becomes false, we stop fetching services
    boolean fetching_service_status = true;

    string current_service = "";

    // index into table column for each runlevel
    map runlevel2tableindex = $[];

    Wizard::CreateDialog ();
    Wizard::DisableAbortButton ();
    Wizard::RestoreHelp (getHelpProgress ());

    RunlevelEd::Read ();

    integer id = 2;
    foreach (`i, RunlevelEd::runlevels, ``{
	runlevel2tableindex = add (runlevel2tableindex, i, id);
	id = id + 1;
    });

    //
    // maps for sequencer
    //
    map aliases = $[
	"start" :            ``(runMain ()),
	"complex" :          ``(allInOne ()),
	];

    map main_sequence = $[
	"ws_start" : "start",
	"start" : $[ `next : `finish , `abort : `abort, `edit : "complex", ],
	"complex" : $[ `next : `finish, `abort : `abort ],
	];

    if (`finish == WizardSequencer (aliases, main_sequence))
    {
	RunlevelEd::Write ();
	// message popup
	Popup::Message (_("The new configuration \nwas saved successfully."));
    }
    UI::CloseDialog ();
}
