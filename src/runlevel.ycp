/**
 * File:
 *  runlevel.ycp
 * Module:
 *  Runlevel editor.
 * Summary:
 *  Main file of runlevel editor.
 * Authors:
 *  Petr Blahos <pblahos@suse.cz>, 2001
 *
 * $Id$
 *
 * Main file of runlevel editor.   
 */
{
    textdomain "runlevel";

    include "ui/wizard_dialog.ycp";
    include "ui/progress.ycp";
    include "wizard/sequencer.ycp";

    // function for reading/manipulating services
    include "runlevel/services.ycp";
    include "runlevel/ui.ycp";

    
    map start_variables_disabled = $[];
    map start_variables_all = $[];
    include "runlevel/starts.ycp";
    
    //
    // globals
    //

    // you know what this means
    boolean do_abort_now = false;

    // We read service status when dialog with services is shown.
    // We read status for services taken from list of services (service_list)
    // and then update map services.
    integer fetching_service_index = 0;
    // when fetching_service_status becomes false, we stop fetching services
    boolean fetching_service_status = true;

    // list of runlevels. e.g. [ "0", "1", "2", "S" ]
    // taken from ls /etc/init.d/rc?.d
    list runlevels = [];

    // index into table column for each runlevel
    map runlevel2tableindex = $[];

    // default runlevel and original value (for dirtyness comparison)
    string default_runlevel = "";
    string default_runlevel_orig = "";
    
    // all services detected in /etc/init.d
    // there is "service" : map
    // map will be described later
    /***
	Main structure containing all settings in runlevel editor is map with following tuples:<br>
	<pre>
      "servicename" : $[
	"defstart" : [ "2", "3", "5", ], // Default-Start comment
	"defstop"  : [ "0", "1", "6", ], // Default-Stop  comment
	
	"reqstart" : [ "$network", "portmap" ], // Required-Start comment
	"reqstop"  : [ "$network", "portmap" ], // Required-Stop  comment

	"description" : "text...",       // Description comment

	"start" : [ "3", "5", ], // which runlevels service is really started/stopped in
	"stop"  : [ "3", "5", ], // read from /etc/init.d/rc?.d/* links

	"started" : "0", // return from rcservice status

	"START" : true, // enabled(true)/disabled(false) by START_VARIABLE?

	"dirty" : false, // was the entry changed?
      ]
      </pre>
     */
    map services = $[];
    list service_list = [];

    // services that have been chaged in this run
    // are placed to this map to 
    map changed_services = $[];
    
    // currently selected service
    string current_service = "";

    // current runlevel
    string current_runlevel = "?";

    UI (``{
	CreateWizardDialog ();
	DisableWizardAbortButton ();
    });

    ProgressSetup (_("Initializing runlevel editor. Please wait."),
		   " ",
		   11,
		   [],
		   // help text
		   getHelpProgress ());

    // read list of runlevels
    readRunlevelList ();
    ProgressStep (2);
    // read current runlevel
    readCurrentRunlevel ();
    ProgressStep (4);
    // read default runlevel
    readDefaultRunlevel ();
    ProgressStep (6);

    // read scripts and find out when they are to run
    getScripts ();
    ProgressStep (10);

    //
    // maps for sequencer
    //
    map aliases = $[
	"start" :            ``(runMain ()),
	"complex" :          ``(allInOne ()),
	];

    map main_sequence = $[
	"ws_start" : "start",
	"start" : $[ `next : `finish , `abort : `abort, `edit : "complex", ],
	"complex" : $[ `next : `finish, `abort : `abort ],
	];

    WizardSequencer (aliases, main_sequence);
    UI::CloseDialog ();
}
