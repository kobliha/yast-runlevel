/**
 * File:
 *   toposort.ycp
 *
 * Module:
 *   Runlevel Editor
 *
 * Summary:
 *   Topological sorting for script dependencies
 *
 * Authors:
 *   Martin Vidner <mvidner@suse.cz>
 *
 * $Id$
 */

{
    /**
     * Topologically sort a directed acyclic graph, ie. linearize a
     * partial ordering.
     * (what if the graph is a multigraph??)
     * @param g A DAG as a map:
     *  nodes are keys, values are lists of nodes that are reached
     *  by an edge from the respective key.
     * @return [out, rest]<br>
     *  out: a list containing the keys of the map in topological order<br>
     *  rest: a list, empty if the graph was acyclic, otherwise it is
     *   a superset of the nodes forming the cycle
     *   and "out" is a partial result
     */
    global define list(string) TopologicalSort (map g) ``{
	list out = [];
	map in_degree = mapmap (any vertex, list targets, g, ``([vertex, 0]));
	foreach (any vertex, list targets, g, ``{
	    foreach (any target, targets, ``{
		in_degree[target] = in_degree[target]:0 + 1;
	    });
	});

	// cycle
	while (size (in_degree) > 0)
	{
	    // get the vertices that can go next because they have zero in degree
	    map next_m = filter (any vertex, integer d, in_degree, ``(d == 0));
	    if (size (next_m) == 0)
	    {
		// the graph is cyclic!
		break;
	    }
	    foreach (any vertex, any dummy, next_m, ``{
		out = add (out, vertex);
	    });
	    // remove the vertices
	    in_degree = filter (any vertex, integer d, in_degree, ``(d != 0));
	    // remove the edges that were leading from them
	    foreach (any vertex, any dummy, next_m, ``{
		foreach (any target, g[vertex]:[], ``{
		    in_degree[target] = in_degree[target]:0 - 1;
		});
	    });
	}

	list rest = maplist (any k, any v, in_degree, ``( k )); //mapkeys
	if (size (rest) > 0)
	{
	    y2error ("Cyclic subgraph found, remainder has %1 nodes: %2",
		     size (rest), rest);
	}
	return [out, rest];
    }

    /**
     * Make a subgraph of g, starting at start
     * @param g A directed acyclic graph as a map:
     *  nodes are keys, values are lists of nodes that are reached
     *  by an edge from the respective key.
     * @param start starting node
     * @return the reachable subgraph
     */
    global define map ReachableSubgraph (map g, any start) ``{
	// a breadth-first search
	map result = $[];
	// seen and next_layer are sets, realized as maps with dummy values
	map seen = $[];
//	map next_layer = $[start: true]; //AARGH! YCP!! #16701
	map next_layer = add ($[], start, true);
	do
	{
	    map current_layer = next_layer;
	    next_layer = $[];
	    foreach (any node, any dummy, current_layer, ``{
		// action: add the node and edges
		result = add (result, node, g[node]:[]);

		// next
		seen = add (seen, node, true);
		// targets from this node.
		// filter the ones already seen (including itself)
		list targets = filter (any target, g[node]:[],
				       ``( !haskey (seen, target)));
		foreach (any target, targets, ``{
		    next_layer = add (next_layer, target, true);
		});
	    });
	}
	while (size (next_layer) > 0);
	return result;
    }

    /**
     * Reverse edges of an oriented graph
     * @param g a graph
     * @return reversed graph
     */
    global define map ReverseGraph (map g) ``{
	// initialize, otherwise we miss nodes with zero in-degree
	map rev = mapmap (any node, any dummy, g, ``( [node, []] ));
	foreach (any source, list targets, g, ``{
	    foreach (any target, targets, ``{
		rev[target] = add (rev[target]:[], source);
	    });
	});
	return rev;
    }
}
